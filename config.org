#+title: Config
#+PROPERTY: header-args :tangle config.el

ç°åœ¨æˆ‘ç”¨çš„ä¸»é¢˜æ˜¯â€¦â€¦

#+begin_src emacs-lisp
(setq doom-theme 'ef-arbutus)
#+end_src

* å¯åŠ¨
ä»Šæ—¥æ–¹çŸ¥æˆ‘æ˜¯æˆ‘ï¼š

#+begin_src emacs-lisp
(setq user-full-name "SouthFox"
      user-mail-address "master@southfox.me")
#+end_src

** æ¨ªå¹…
æ›¿æ¢ =Doom Emacs= çš„æ¨ªå¹…ï¼Œç”¨ä¸€åªç‹ç‹¸ï¼

#+begin_src emacs-lisp
(defun doom-dashboard-draw-ascii-emacs-banner-fn ()
  (let* ((banner
          '("                                                                   ,           "
            "                                                             _.-=;~ /_         "
            "                                                          _-~   '     ;.       "
            "                                                      _.-~     '   .-~-~`-._   "
            "                                                _.--~~:.             --.____88 "
            "                              ____.........--~~~. .' .  .        _..-------~~  "
            "                     _..--~~~~               .' .'             ,'              "
            "                 _.-~                        .       .     ` ,'                "
            "               .'                                    :.    ./                  "
            "             .:     ,/          `                   ::.   ,'                   "
            "           .:'     ,(            ;.                ::. ,-'                     "
            "          .'     ./'.`.     . . /:::._______.... _/:.o/                        "
            "         /     ./'. . .)  . _.,'               `88;?88|                        "
            "       ,'  . .,/'._,-~ /_.o8P'                  88P ?8b                        "
            "    _,'' . .,/',-~    d888P'                    88'  88|                       "
            " _.'~  . .,:oP'        ?88b              _..--- 88.--'8b.--..__                "
            ":     ...' 88o __,------.88o ...__..._.=~- .    `~~   `~~      ~-._ Fox! _.    "
            "`.;;;:='    ~~            ~~~                ~-    -       -   -               "))
         (longest-line (apply #'max (mapcar #'length banner))))
    (put-text-property
     (point)
     (dolist (line banner (point))
       (insert (+doom-dashboard--center
                +doom-dashboard--width
                (concat
                 line (make-string (max 0 (- longest-line (length line)))
                                   32)))
               "\n"))
     'face 'doom-dashboard-banner)))

;(unless (display-graphic-p) ; for some reason this messes up the graphical splash screen atm
  (setq +doom-dashboard-ascii-banner-fn #'doom-dashboard-draw-ascii-emacs-banner-fn) ;)
#+end_src

* org-mode
å°† =org-log-into-drawer= è®¾ä¸º =t= åï¼Œå®Œæˆä»»åŠ¡åçš„æ—¶é—´æˆ³å°†ä¼šæ”¶èµ·ï¼Œ
è¿™æ ·ä¸€äº›æ¯å¤©å®Œæˆçš„ä»»åŠ¡å°±ä¸ä¼šè¢«ç›¸å…³æ—¶é—´æˆ³å æ»¡ã€‚

#+begin_src emacs-lisp
(after! org
  (setq org-directory "~/org/"
        org-image-actual-width '(400)
        org-link-search-must-match-exact-headline nil
        org-log-done 'time
        org-log-into-drawer t))

#+end_src

doom emacs é‡Œçš„è¿œç¨‹å›¾ç‰‡ä¿®å¤ï¼š

#+begin_src elisp
;; DOOM hack fix https://github.com/doomemacs/doomemacs/issues/8164
(after! org
  (defun org-image-update-overlay (file link &optional data-p refresh)
    "Create image overlay for FILE associtated with org-element LINK.
If DATA-P is non-nil FILE is not a file name but a string with the image data.
If REFRESH is non-nil don't download the file but refresh the image.
See also `create-image'.
This function is almost a duplicate of a part of `org-display-inline-images'."
    (when (or data-p (file-exists-p file))
      (let ((width
             ;; Apply `org-image-actual-width' specifications.
             (cond
              ((eq org-image-actual-width t) nil)
              ((listp org-image-actual-width)
               (or
                ;; First try to find a width among
                ;; attributes associated to the paragraph
                ;; containing link.
                (let ((paragraph
                       (let ((e link))
                         (while (and (setq e (org-element-property
                                              :parent e))
                                     (not (eq (org-element-type e)
                                              'paragraph))))
                         e)))
                  (when paragraph
                    (save-excursion
                      (goto-char (org-element-property :begin paragraph))
                      (when
                          (re-search-forward
                           "^[ \t]*#\\+attr_.*?: +.*?:width +\\(\\S-+\\)"
                           (org-element-property
                            :post-affiliated paragraph)
                           t)
                        (string-to-number (match-string 1))))))
                ;; Otherwise, fall-back to provided number.
                (car org-image-actual-width)))
              ((numberp org-image-actual-width)
               org-image-actual-width)))
            (old (get-char-property-and-overlay
                  (org-element-property :begin link)
                  'org-image-overlay)))
        (if (and (car-safe old) refresh)
            (image-flush (overlay-get (cdr old) 'display))
          (let ((image (create-image file
                                     (and (image-type-available-p 'imagemagick)
                                          width
                                          'imagemagick)
                                     data-p
                                     :width width)))
            (when image
              (let* ((link
                      ;; If inline image is the description
                      ;; of another link, be sure to
                      ;; consider the latter as the one to
                      ;; apply the overlay on.
                      (let ((parent
                             (org-element-property :parent link)))
                        (if (eq (org-element-type parent) 'link)
                            parent
                          link)))
                     (ov (make-overlay
                          (org-element-property :begin link)
                          (progn
                            (goto-char
                             (org-element-property :end link))
                            (skip-chars-backward " \t")
                            (point)))))
                (overlay-put ov 'display image)
                (overlay-put ov 'face 'default)
                (overlay-put ov 'org-image-overlay t)
                (overlay-put
                 ov 'modification-hooks
                 (list 'org-display-inline-remove-overlay))
                (push ov org-inline-image-overlays)
                ov)))))))

  (defun org-display-user-inline-images (&optional _include-linked _refresh beg end)
    "Like `org-display-inline-images' but for image data links.
_INCLUDE-LINKED and _REFRESH are ignored.
Restrict to region between BEG and END if both are non-nil.
Image data links have a :image-data-fun parameter.
\(See `org-link-set-parameters'.)
The value of the :image-data-fun parameter is a function
taking the PROTOCOL, the LINK, and the DESCRIPTION as arguments.
If that function returns nil the link is not interpreted as image.
Otherwise the return value is the image data string to be displayed.

Note that only bracket links are allowed as image data links
with one of the formats
 [[PROTOCOL:LINK]]
or
 [[PROTOCOL:LINK][DESCRIPTION]]
are recognized."
    (interactive)
    (when (and (called-interactively-p 'any)
               (use-region-p))
      (setq beg (region-beginning)
            end (region-end)))
    (when (display-graphic-p)
      (org-with-wide-buffer
       (goto-char (or beg (point-min)))
       (when-let ((image-data-link-parameters
		   (cl-loop for link-par-entry in org-link-parameters
			    with fun
			    when (setq fun (plist-get (cdr link-par-entry) :image-data-fun))
			    collect (cons (car link-par-entry) fun)))
		  (image-data-link-re (regexp-opt (mapcar 'car image-data-link-parameters)))
		  (re (format "\\[\\[\\(%s\\):\\([^]]+\\)\\]\\(?:\\[\\([^]]+\\)\\]\\)?\\]"
			      image-data-link-re)))
         (while (re-search-forward re end t)
           (let* ((protocol (match-string-no-properties 1))
		  (link (match-string-no-properties 2))
		  (description (match-string-no-properties 3))
		  (image-data-link (assoc-string protocol image-data-link-parameters))
		  (el (save-excursion (goto-char (match-beginning 1)) (org-element-context)))
		  image-data)
             (when el
               (setq image-data
                     (or (let ((old (get-char-property-and-overlay
                                     (org-element-property :begin el)
                                     'org-image-overlay)))
                           (and old
                                (car-safe old)
                                (overlay-get (cdr old) 'display)))
		         (funcall (cdr image-data-link) protocol link description)))
               (when image-data
                 (let ((ol (org-image-update-overlay image-data el t t)))
                   (when (and ol description)
                     (overlay-put ol 'after-string description)))))))))))

  (advice-add #'org-display-inline-images :after #'org-display-user-inline-images))
#+end_src

** org-capture
ä¸€äº›æ•è·æ¨¡æ¿å’Œè·¯å¾„è®¾ç½®ï¼Œä½†è¯´å®åœ¨è‡ªä»ç”¨äº† =org-roam= é‚£è¾¹è¿™è¾¹å°±æ²¡æ€ä¹ˆç®¡äº†ã€‚

#+begin_src emacs-lisp
(setq org-agenda-files '("~/Sync/org/GTD/inbox.org"
                         "~/Sync/org/GTD/tickler.org"))

(after! org
  (setq org-capture-templates '(("t" "Todo [inbox]" entry
                               (file "~/Sync/org/GTD/inbox.org")
                               "* TODO %i%? \n SCHEDULED: %t")
                              ("T" "Tickler" entry
                               (file+headline "~/Sync/org/GTD/tickler.org" "Tickler")
                               "* %i%? \n %U"))))
#+end_src

** org-roam
ä¸»è¦æ˜¯ä¸€äº›æ•è·æ¨¡æ¿è®¾ç½®ï¼Œæ–‡ä»¶å‘½ååŠ ä¸Šæ—¶é—´è¿˜æ˜¯å› ä¸ºå¯¼å‡ºåˆ°æ•°å­—èŠ±å›­æ—¶ä¼šæ”¾åœ¨ä¸€ä¸ªæ–‡ä»¶å¤¹é‡Œï¼Œ
è€Œä¸ºäº†é¿å…é‡åã€‚

#+begin_src emacs-lisp
(setq org-roam-directory "~/Sync/org/Note/org-roam")
(setq org-roam-capture-templates
      '(("m" "main" plain
	 "%?"
	 :if-new (file+head "main/%<%Y%m%d%H%M%S>-${slug}.org"
                            "#+title: ${title}\n#+date: %T\n#+hugo_section: main\n")
	 :immediate-finish t
	 :unnarrowed t)
	("r" "references" plain "%?"
	 :if-new
	 (file+head "references/%<%Y%m%d%H%M%S>-${title}.org" "#+title: ${title}\n#+date: %T\n#+hugo_section: references\n")
	 :immediate-finish t
	 :unnarrowed t)
	("a" "article" plain "%?"
	 :if-new
	 (file+head "articles/%<%Y%m%d%H%M%S>-${title}.org" "#+title: ${title}\n#+date: %T\n#+filetags: :article:\n#+hugo_section: articles\n")
	 :immediate-finish t
	 :unnarrowed t)))

(setq org-roam-dailies-capture-templates
      '(("d" "default" entry
         "* %?"
         :target (file+head "%<%Y-%m-%d>.org"
                            "#+title: %<%Y-%m-%d>\n#+date: %T\n#+hugo_section: daily\n"))))

(with-eval-after-load 'ox-hugo
  (setq org-hugo-anchor-functions '(org-hugo-get-id
                                    org-hugo-get-heading-slug
                                    org-hugo-get-md5)))
#+end_src

*** Garden
æ•°å­—èŠ±å›­ï¼Œå¼€å§‹ç§æ¤æ€æƒ³â€¦â€¦

#+begin_src emacs-lisp
(defcustom org-roam-garden-file "~/Sync/org/GTD/garden.org"
  "The file used by roam garden."
  :group 'org-roam
  :type 'string)

(add-to-list 'org-agenda-files org-roam-garden-file)
#+end_src

#+begin_src emacs-lisp
(defun org-roam-garden-plant ()
  (interactive)
  (save-excursion
    (goto-char (point-min))
    (let* ((node (org-roam-node-at-point))
           (title (org-roam-node-title node))
           (file (org-roam-node-file node)))
      (org-set-property "GARDEN-STATE" "seedling")
      (org-roam-update-garden-file title file))
    (save-buffer)))

(defun org-roam-update-garden-file (heading file-path)
  (with-current-buffer (find-file-noselect org-roam-garden-file)
    (let ((buffer (current-buffer)))
      (if (org-find-exact-headline-in-buffer heading)
          (message "Plant exists, skip...")
        (goto-char (point-max))
        (insert (concat "* " heading "\n[[" file-path "]]"))
        (org-schedule nil (format "<%s 22:00 ++3d >" (format-time-string "%Y-%m-%d %u")))
        (save-buffer)))))

(defun my/org-roam-filter-by-properties (properties-name)
  (lambda (node)
    (assoc properties-name (org-roam-node-properties node))))

(defun my/org-roam-list-notes-by-properties (properties-name)
  (seq-filter
   (my/org-roam-filter-by-properties properties-name)
   (org-roam-node-list)))

(defun my/org-roam-garden-dashboard--build-item (node)
  (concat "\n** " (org-roam-node-title node) "\n[[" (org-roam-node-file node) "]]"))

(defun my/org-roam-garden-dashboard ()
  (interactive)
  (with-current-buffer (get-buffer-create "*Garden*")
    (erase-buffer)
    (let ((filtered-node (my/org-roam-list-notes-by-properties "GARDEN-STATE")))
      (insert "* ğŸŒ± seedling")
      (insert (string-join (mapcar #'my/org-roam-garden-dashboard--build-item
                                   filtered-node))))
    (org-mode)
    (switch-to-buffer (current-buffer))))
#+end_src

** org-fragtog
å¥½åƒæ˜¯ =latex= ç›¸å…³çš„é¢„è§ˆåŒ…â€¦â€¦ï¼Ÿ

#+begin_src emacs-lisp
(use-package! org-fragtog
  :hook (org-mode . org-fragtog-mode))
#+end_src

** roam-publish
ä¸€äº›ä»ä¸–ç•Œå„åœ°æ‹¼å‡ºæ¥çš„ä»£ç ï¼Œè¿˜æœ‰ä¸€äº›è‡ªå·±å•çº¯å †å è°ƒç”¨çš„å‡½æ•°ï¼Œ
æˆ‘çœŸè¯¥ç³»ç»Ÿå­¦å­¦ =elisp= +1 ã€‚

#+begin_src emacs-lisp
(setq org-hugo-base-dir "~/Documents/roam-publish/")

(defun my/org-roam-filter-by-tag (tag-name)
  (lambda (node)
    (member tag-name (org-roam-node-tags node))))

(defun my/org-roam-list-notes-by-tag (tag-name)
  (mapcar #'org-roam-node-file
          (seq-filter
           (my/org-roam-filter-by-tag tag-name)
           (org-roam-node-list))))

(defun my/org-roam-export-all ()
  "Re-exports all Org-roam files to Hugo markdown."
  (interactive)
  (dolist (org-file (my/org-roam-list-notes-by-tag "publish"))
  ;(dolist (org-file (directory-files-recursively org-roam-directory "\.org$"))
    (with-current-buffer (find-file org-file)
        (my/org-roam-publish t))))

(defun my/insert--backlinks (&optional heading-func)
  (if-let* ((file-title (org-get-title))
            (backlinks (org-roam-backlinks-get (org-roam-node-at-point) :unique t)))
      (save-excursion
        (when heading-func
          (funcall heading-func))
        (insert " åå‘é“¾æ¥\n")
        (dolist (backlink backlinks)
          (let* ((source-node (org-roam-backlink-source-node backlink))
                 (point (org-roam-backlink-point backlink)))
            (insert
             (format "- [[id:%s][%s]]\n"
                     (org-roam-node-id source-node)
                     (org-roam-node-title source-node))))))))

(defun my/insert-backlinks ()
  (goto-char (point-min))
  (org-shifttab 0)
  (while (= 0 (org-next-visible-heading 1))
    (when (assoc "ID" (org-entry-properties))
      (my/insert--backlinks
       (lambda ()
         (org-insert-subheading t)))))
  (goto-char (point-min))
  (my/insert--backlinks
   (lambda ()
     (goto-char (point-max))
     (insert "* "))))

(defun my/extract-org-roam-id (origin-filename)
  (save-excursion
    (goto-char (point-min))
    (let ((matche '()))
      (while (re-search-forward "\\[\\[id:\\(.*?\\)]\\[.*?\\]\\]" nil t)
        (when-let* ((uuid (match-string-no-properties 1))
                    (roam-node (org-roam-node-from-id uuid))
                    (file-name (car (org-roam-id-find uuid))))
          (when (member "publish" (org-roam-node-tags roam-node))
            (push file-name matche))))
      (delete origin-filename (seq-uniq matche)))))

(defun my/org-roam-publish (&optional skip-publish skip-update-backlinks)
  "Publish current file"
  (interactive)
  (goto-char (point-min))
  (org-roam-update-org-id-locations)
  (org-roam-tag-add (list "publish"))
  (unless skip-update-backlinks
    (org-roam-set-keyword "hugo_lastmod" (format-time-string "%Y-%m-%d %H:%M:%S")))
  (save-buffer)
  (let ((publish-content (buffer-string))
        (origin-filename (buffer-file-name))
        (filename (concat (file-name-base (buffer-file-name)) ".org")))
    (with-temp-buffer
      (erase-buffer)
      (insert publish-content)
      (goto-char (point-min))
      (org-mode)
      (org-roam-set-keyword "EXPORT_FILE_NAME" filename)
      (org-roam-tag-remove (list "publish"))
      (if-let ((headline (org-find-exact-headline-in-buffer "æ€»ç»“")))
          (progn
            (goto-char headline)
            (org-cut-subtree)))
      (if-let ((headline (org-find-exact-headline-in-buffer "æ¢¦è®°")))
          (progn
            (goto-char headline)
            (org-cut-subtree)))
      (my/insert-backlinks)
      (unless skip-update-backlinks
        (let ((back-files (my/extract-org-roam-id origin-filename)))
          (dolist (back-file back-files)
            (message back-file)
            (with-current-buffer (find-file-noselect back-file)
              (my/org-roam-publish t t)))))

      (org-hugo-export-wim-to-md)))
  (unless skip-publish
    (async-shell-command (concat
                          "cd " org-hugo-base-dir
                          " && " "git add ."
                          (if (yes-or-no-p "Push now?")
                              (concat " && " "git commit -m '[post] new post'"
                                      " && " "git push"))) "*Messages*"))
  (message "publish new post!"))

(defun my/org-roam-creat-node ()
  "creat node and add publish."
  (interactive)
  (org-id-get-create)
  (save-buffer)
  (my/org-roam-publish))
#+end_src

** org-clip
#+begin_src emacs-lisp
(setq org-cliplink-transport-implementation 'curl)
(setq org-cliplink-curl-transport-arguments '("-L" "-x" "http://127.0.0.1:10809"))
#+end_src
* å®
** cond-let
ç»ˆäºçŸ¥é“ä¸ºä»€ä¹ˆæ²¡äººæƒ³å†™è¿™ä¸ªå®äº†ï¼Œæ‹¬å·æ˜¯çœŸå¾—å¤šå•Šã€‚

#+begin_src emacs-lisp
(defmacro cond-let (&rest forms)
  (declare (debug t))
  (if (not (equal forms 'nil))
      `(let* ,(setq varlist (internal--build-bindings (caar forms)))
         (if-let ,(car (last varlist))
             ,(cadar forms)
           (cond-let ,@(cdr forms))))))
#+end_src

* æ‚é¡¹
=word-wrap-by-category= æ˜¯ä¸­æ–‡ä¼˜åŒ–ï¼Œå½“ä¸€è¡Œæœ‰è‹±æ–‡å’Œä¸­æ–‡æ—¶ä¸æŠ˜è¡Œã€‚

#+begin_src emacs-lisp
(setq word-wrap-by-category t)

(setq doom-unicode-font (font-spec :family "Noto Color Emoji"))
(add-to-list 'default-frame-alist '(height . 35))
(add-to-list 'default-frame-alist '(width . 102))
#+end_src

ä¸¤ä¸ªå¿«ä¹çš„äº‹æƒ…é‡åˆåˆ°ä¸€èµ·â€¦â€¦

#+begin_src emacs-lisp
(defun fox/switch-project-magit-status (&optional arg)
  (interactive "P")
  (let ((projects (projectile-relevant-known-projects)))
    (if projects
        (projectile-completing-read
         "Switch to project: " projects
         :action (lambda (project)
                   (magit-status-setup-buffer project)))
      (user-error "There are no known projects"))))

(map! :leader
      :prefix ("g" . "git")
      :desc "Switch magit status"          "p" #'fox/switch-project-magit-status)
#+end_src

** Shell
å°†ä½¿ç”¨çš„ç»ˆç«¯åˆ‡æ¢åˆ° =zsh= ã€‚

#+begin_src emacs-lisp
(setq shell-file-name (expand-file-name "~/.guix-home/profile/bin/zsh")
      vterm-max-scrollback 5000)
#+end_src

** TODO æ´¾å¯¹é¹¦é¹‰
å¿«æ·é”®å’Œè¯å…¸ï¼Œä½†è¯´å®åœ¨æ²¡ç”¨è¿‡è¿™åŠŸèƒ½ï¼Œæ¯•ç«Ÿè¿™ä¸ªåŒ…çš„é‡ç‚¹å½“ç„¶æ˜¯åœ¨åŠ¨å›¾ä¸Šã€‚

#+begin_src emacs-lisp
(parrot-mode 1)
(define-key evil-normal-state-map (kbd "[r") 'parrot-rotate-prev-word-at-point)
(define-key evil-normal-state-map (kbd "]r") 'parrot-rotate-next-word-at-point)

(setq parrot-rotate-dict
      '(
        (:rot ("yes" "no") :caps t :upcase t)
        (:rot ("&" "|"))
        (:rot ("begin" "end") :caps t :upcase t)
        (:rot ("enable" "disable") :caps t :upcase t)
        (:rot ("enter" "exit") :caps t :upcase t)
        (:rot ("forward" "backward") :caps t :upcase t)
        (:rot ("front" "rear" "back") :caps t :upcase t)
        (:rot ("get" "set") :caps t :upcase t)
        (:rot ("high" "low") :caps t :upcase t)
        (:rot ("in" "out") :caps t :upcase t)
        (:rot ("left" "right") :caps t :upcase t)
        (:rot ("min" "max") :caps t :upcase t)
        (:rot ("on" "off") :caps t :upcase t)
        (:rot ("start" "stop") :caps t :upcase t)
        (:rot ("true" "false") :caps t :upcase t)
        (:rot ("&&" "||"))
        (:rot ("==" "!="))
        (:rot ("if" "else" "elif"))
        (:rot ("ifdef" "ifndef"))
        ))

#+end_src

å®šä¹‰ä¸€ä¸ªå¯ä»¥é’©ä½å¤šä¸ª =hook= çš„å‡½æ•°ï¼Œæˆ‘æ„Ÿè§‰æˆ‘ä¸å­¦ =elisp= çš„è¯çœŸä¸è¡Œäº†å•Šâ€¦â€¦

#+begin_src emacs-lisp
(defun my-add-to-multiple-hooks (function hooks)
  (mapc (lambda (hook)
          (add-hook hook function))
        hooks))
#+end_src

ä¹‹åå°†åœ¨å®Œæˆä¸€ä¸ªç•ªèŒ„é’Ÿæ’­æ”¾ç‚¹èµåŠ¨ç”»ï¼Œä¿å­˜ä¸€ä¸ªæ–‡ä»¶æ—¶æ’­æ”¾ =emacs= åŠ¨ç”»ç­‰ç­‰ï¼Œ
æ„Ÿè§‰çœŸè¯¥å­¦å­¦ =elisp= äº†å•Šï¼Œå¿…é¡»å¾—é‡æ„è¿™æ®µäº†ã€‚

#+begin_src emacs-lisp
(defun my-parrot-thumbsup-play ()
  (parrot-set-parrot-type 'thumbsup)
  (parrot-start-animation))

(defun my-parrot-emacs-play ()
  (parrot-set-parrot-type 'emacs)
  (parrot-start-animation))

(defun my-parrot-rotating-play ()
  (parrot-set-parrot-type 'rotating)
  (parrot-start-animation))

(my-add-to-multiple-hooks
 'my-parrot-thumbsup-play
 '(org-after-todo-state-change-hook
   org-clock-in-hook
   org-timer-done-hook
   git-commit-post-finish-hook))

(my-add-to-multiple-hooks
 'my-parrot-emacs-play
 '(after-save-hook
   find-file-hook))
#+end_src

** EVIL
è€¶ï¼Œå •å…¥é»‘æš—ï¼

è®¾ç½® =orderless= ä½¿ç”¨ä½¿ç”¨æ‹¼éŸ³é¦–å­—æ¯æœç´¢ï¼Œpyszm æœ€æœ‰ç”¨çš„ä¸€é›†ã€‚

#+begin_src elisp
(use-package evil-pinyin
  :after (evil orderless)
  :autoload evil-pinyin--build-regexp-string
  :init
  (setq-default evil-pinyin-scheme 'simplified-ziranma-all)
  (defun completion--regex-pinyin (str)
    (orderless-regexp (evil-pinyin--build-regexp-string str)))
  (add-to-list 'orderless-matching-styles 'completion--regex-pinyin)
  :config
  (global-evil-pinyin-mode))
#+end_src
** Radio Garden

#+begin_src emacs-lisp
(use-package! radio-garden
  :after
  emms-player-mpv
  :config
  (setq! emms-player-mpv-update-metadata t
         radio-garden-proxy-option "http://127.0.0.1:10809")
  (defun my/insert-radio-current-play ()
    (interactive)
    (with-current-buffer (find-file-noselect (org-roam-node-file (org-roam-node-from-title-or-alias "Music")))
      (goto-char (point-max))
      (insert (concat "- [ ] ")
              (cdr (assoc 'info-title (emms-playlist-current-selected-track))))
      (save-buffer))))
#+end_src

** å…¶å®ƒæ–‡ä»¶
å­˜æ”¾ä¸€äº› =token= çš„ç§˜å¯†æ–‡ä»¶ï¼Œåº”è¯¥æ³¨æ„ä¸èƒ½éš =git= ä¸Šä¼ æˆ–è€…åŒæ­¥ã€‚

#+begin_src emacs-lisp
(load! "secrets")
(load! "elisp/hy")
(load! "elisp/aaii")
(setq-default custom-file (expand-file-name "secrets.el" doom-user-dir))
(when (file-exists-p custom-file)
  (load custom-file))
#+end_src

* æ–‡çŒ®ç®¡ç†
** Citar
#+begin_src emacs-lisp
(setq bibtex-completion-pdf-field "File")
(setq bibtex-completion-bibliography '("~/Sync/Ebook/catalog.bib"))
(setq citar-bibliography '("~/Sync/Ebook/catalog.bib"))
#+end_src

* å·¥å…·
** Consult
#+begin_src elisp
;; Adopted from counsel-omni:
;; URL https://github.com/armindarvish/consult-omni
(require 'consult)

(cl-defun my/consult-fox--apps-format-candidates (&rest args &key title path snippet visible &allow-other-keys)
  "Formats the candidates of `consult-fox-apps' withn ARGS

Description of Arguments:

  SOURCE  a string; the name to use (e.g. â€œAppsâ€)
  QUERY   a string; the query input from the user
  TITLE   a string; the title of the App (name of an application)
  PATH    a string; the filepath to the application
  SNIPPET a string; the description of the app (from Desktop Entry)
  VISIBLE a boolean; whether the applicaiton is visible
          (this can be derived from DesktopEntry)
  FACE    a symbol; the face to use for TITLE"
  (let* (
         (directory (and path (file-name-directory path)))
         (title-str (propertize title))
         (str (concat title-str
                      (unless visible "\s[Hidden App]")
                      (when snippet (concat "\t" snippet))
                      (when directory (concat "\t" directory))
                      )))
    str))

(defun my/consult-fox--apps-get-desktop-apps ()
  "Return a list of system applications. "
  (save-match-data
    (let ((paths (remove nil (mapcar (lambda (dir)
                                       (let ((path (and (stringp dir) (file-exists-p dir) (file-truename (expand-file-name "applications" dir)))))
                                         (and (stringp path) path)))
                                     (append (if (fboundp 'xdg-data-dirs) (xdg-data-dirs)
                                               (let ((path (getenv "XDG_DATA_DIRS")))
                                                 (if (or (null path) (string= path ""))
                                                     nil
                                                   (parse-colon-path path))))
                                             (list
                                              "/usr/share"
                                              "/usr/local/share"))))))
      (when (listp paths)
        (setq consult-fox-apps-cached-apps
              (cl-remove-duplicates
               (apply #'append (mapcar
                                (lambda (path)
                                  (when (file-exists-p path)
                                    (directory-files path t ".*\\.desktop$" t)))
                                paths))))))))

(defun my/consult-fox--apps-parse-app-file (file)
  "Parse a desktop entry FILE.

Returns
 - name: the name of the application
 - comment: description of the application
 - exec: the executable for application

Adopted from `counsel-linux-app--parse-file' in counsel:
URL https://github.com/abo-abo/swiper/blob/master/counsel.el"
  (save-match-data
    (with-temp-buffer
      (insert-file-contents file)
      (goto-char (point-min))
      (let ((start (re-search-forward "^\\[Desktop Entry\\] *$" nil t))
            (end (re-search-forward "^\\[" nil t))
            (visible t)
            name comment exec)
        (catch 'break
          (unless start
            (throw 'break nil))
          (goto-char start)
          (when (re-search-forward "^\\(Hidden\\|NoDisplay\\) *= *\\(1\\|true\\) *$" end t)
            (setq visible nil))
          (setq name (match-string 1))
          (goto-char start)
          (unless (re-search-forward "^Type *= *Application *$" end t)
            (throw 'break nil))
          (setq name (match-string 1))
          (goto-char start)
          (unless (re-search-forward "^Name *= *\\(.+\\)$" end t)
            (throw 'break nil))
          (setq name (match-string 1))
          (goto-char start)
          (when (re-search-forward "^Comment *= *\\(.+\\)$" end t)
            (setq comment (match-string 1)))
          (goto-char start)
          (unless (re-search-forward "^Exec *= *\\(.+\\)$" end t)
            ;; Don't warn because this can technically be a valid desktop file.
            (throw 'break nil))
          (setq exec (match-string 1))
          (goto-char start)
          (when (re-search-forward "^TryExec *= *\\(.+\\)$" end t)
            (let ((try-exec (match-string 1)))
              (unless (locate-file try-exec exec-path nil #'file-executable-p)
                (throw 'break nil))))
          (list name comment exec visible))))))

(defun my/consult-fox-apps--get-items (files)
  (save-match-data
    (mapcar (lambda (file)
              (pcase-let* (
                           (`(,name ,comment ,exec ,visible) (my/consult-fox--apps-parse-app-file file))
                           (title (or name (file-name-base file) ""))
                           (app (and (stringp file) (file-exists-p file) (file-name-nondirectory file)))
                           (decorated (funcall #'my/consult-fox--apps-format-candidates :title title :path file :snippet comment :visible visible)))
                (propertize decorated
                            :title title
                            :snippet comment
                            :path file
                            :exec exec
                            :app app)))
            files)))

(defun app-launcher-picker ()
  (let ((app (consult--read (my/consult-fox-apps--get-items (my/consult-fox--apps-get-desktop-apps))
                            :lookup (apply-partially #'consult--lookup-prop :app))))
    (call-process-shell-command (string-join `("gtk-launch"
                                               ,(shell-quote-argument (format "%s" app))
                                               "&")
                                             " "))
    nil))

(defun clipboard-picker ()
  (let* ((clipboard (cl-remove-if #'string-empty-p (split-string (shell-command-to-string "cliphist list") "\n")))
         (selected (consult--read clipboard :sort nil))
         (match (string-match "^[0-9]+" selected)))
    (if match
        (call-process-shell-command (format "cliphist decode %s | wl-copy" (substring selected match (match-end 0))))
      (error "Invalid match"))
    nil))

(defun my/delete-frame-if-neccessary (&rest r)
  (when (and (equal "fox-consult" (frame-parameter nil 'name))
             (not org-roam-capture--node))
      (delete-frame)))

(add-hook 'org-capture-after-finalize-hook #'my/delete-frame-if-neccessary)

(defun my/org-roam-capture ()
  (org-roam-dailies-capture-today)
  (delete-other-windows)
  (toggle-input-method))

(setq my/fox--popup-pick '(clipboard-picker app-launcher-picker my/org-roam-capture))

(defun my/fox-run-launcher (func)
  (interactive
   (list (completing-read "select:" my/fox--popup-pick)))

  (let ((picker-p (cl-search "picker" func)))
    (with-selected-frame
        (make-frame `((name . "fox-consult")
                      (minibuffer . ,(if picker-p 'only t))
                      (fullscreen . 0)   ; no fullscreen
                      (undecorated . t)  ; remove title bar
                      (internal-border-width . 10)
                      (width . 80)
                      (height . 18)))

      (unwind-protect
          (funcall (symbol-function (intern func)))
        (when picker-p
          (delete-frame)
          (when (> (length (window-list)) 1)
            (evil-quit)))))))

(defun my/fox-pick-picker ()
  (interactive)
  (with-selected-frame
      (make-frame '((name . "fox-consult")
                    (fullscreen . 0)                   ; no fullscreen
                    (undecorated . t)                  ; remove title bar
                    (internal-border-width . 10)
                    (width . 80)
                    (height . 18)))
    (let ((func (completing-read "select:" my/fox--popup-pick)))
      (delete-frame)
      (my/fox-run-launcher func))))
#+end_src

** SDCV
ç¿»è¯‘åŒ…ï¼Œè¯å…¸è¦è‡ªå·±ä¸‹ï¼ŒåŒæ—¶ä¸è¦å¿˜äº†å®‰è£… =stardict= å’Œ =sdcv= è¿™ä¸¤ä¸ªè½¯ä»¶ã€‚

#+begin_src emacs-lisp
(setq sdcv-say-word-p t)
(setq sdcv-dictionary-data-dir (expand-file-name "~/.stardict/dic/"))

(map! :leader :desc "sdvc" "z" #'sdcv-search-pointer+)
#+end_src

** TODO Rime
ä¸€ä¸ªæ–‡æœ¬ç¼–è¾‘å™¨ä¹Ÿå¯ä»¥å†…ç½®è¾“å…¥æ³•å‘¢â€¦â€¦

#+begin_src emacs-lisp
(use-package! rime
  :custom
  (default-input-method "rime")
  :config
  (define-key rime-mode-map (kbd "C-i") 'rime-force-enable)
  (setq rime-show-candidate 'posframe)
  (setq rime-disable-predicates
        '(rime-predicate-evil-mode-p
          rime-predicate-after-alphabet-char-p
          rime-predicate-space-after-cc-p
          rime-predicate-current-uppercase-letter-p
          rime-predicate-punctuation-line-begin-p)))
#+end_src

- [ ] ä¼˜åŒ–ä¸­è‹±æ–‡åˆ‡æ¢æ–­è¨€
- [ ] å¦‚ä½•åœ¨ä¸€äº›è¾“å…¥ä¸­è‡ªåŠ¨åˆ‡æ¢åˆ°ä¸­æ–‡ï¼Ÿä¾‹å¦‚ =org-roam=
- [ ] æ”¹é”®ï¼Œç°åœ¨çš„ =C \= ç¡®å®æœ‰ç‚¹æŠ˜ç£¨
** åŠ›æ‰£
ä¸€äº›è¯­è¨€éœ€è¦ä¿å­˜æ–‡ä»¶æ‰æœ‰ =lsp= æ”¯æŒâ€¦â€¦æˆ‘çœŸå¾—éœ€è¦ =lsp=

#+begin_src emacs-lisp
(setq leetcode-prefer-language "racket")
(setq leetcode-save-solutions t)
(setq leetcode-directory "~/Documents/leetcode")
#+end_src
** EMMS / ENEP
#+begin_src elisp
(use-package! enep
  :after
  emms
  :config
  (setq emms-player-next-function #'enep-play-next-like-song))
#+end_src

* ä¿¡æ¯
æ›´å¤šä¿¡æ¯è¯·å‚è€ƒä¸‹é¢è¿™ç¯‡æ–‡ç« ï¼š
[[https://liujiacai.net/blog/2021/03/05/emacs-love-mail-feed/][ä½¿ç”¨ Emacs é˜…è¯»é‚®ä»¶ä¸ RSS - Keep Coding]]
** é‚®ä»¶
#+begin_src emacs-lisp
(add-to-list 'load-path (expand-file-name "/home/southfox/.guix-home/profile/share/emacs/site-lisp/mu4e"))
(after! mu4e
  (setq mu4e-get-mail-command "true")
  (setq mu4e-update-interval (* 21 60)))

(set-email-account! "southfox.me"
  '((mu4e-sent-folder       . "/Sent")
    (mu4e-drafts-folder     . "/southfox.me/Drafts")
    (mu4e-trash-folder      . "/southfox.me/Trash")
    (mu4e-refile-folder     . "/southfox.me/All Mail")
    (smtpmail-smtp-user     . "master@southfox.me")
    (mu4e-compose-signature . "---\nFor mu4e"))
  t)
#+end_src

* Clojure
#+begin_src emacs-lisp
(use-package! flycheck-clj-kondo
  :after (clojure-mode))

(use-package! clj-refactor
  :after (clojure-mode))
#+end_src

* Python
è‡ªåŠ¨åˆ‡æ¢è™šæ‹Ÿç¯å¢ƒï¼Œ poetry åŒ…å¯èƒ½åœ¨å¤„ç†è¿œç«¯æœºå™¨çš„é¡¹ç›®å¯èƒ½ä¼šæœ‰é—®é¢˜æ‰€ä»¥ä»é‚£è¾¹æŠ å‡ºæ¥æ”¾åˆ°è¿™é‡Œã€‚

#+begin_src emacs-lisp
(defvar poetry-venv-list '()
  "List of known poetry virtualenvs.")

(defun my/track-python-virtualenv (&optional _)
  (interactive)
  (when (and buffer-file-name
             (string= nil (file-remote-p default-directory)))
    (cond-let
      (((current-project (locate-dominating-file default-directory ".venv")))
       (pyvenv-activate (concat current-project ".venv")))
      (((current-project (locate-dominating-file default-directory "venv")))
       (pyvenv-activate (concat current-project "venv")))
      (((current-project (locate-dominating-file default-directory "pyproject.toml")))
       (if-let ((poetry-current-project-venv (cdr (assq 'current-project poetry-venv-list))))
           (unless (equal python-shell-virtualenv-path poetry-current-project-venv)
             (pyvenv-activate poetry-current-project-venv))
         (let ((poetry-project-path (locate-dominating-file default-directory "pyproject.toml"))
               (poetry-venv-path (s-trim (shell-command-to-string "env -u VIRTUAL_ENV poetry env info -p"))))
           (pyvenv-activate poetry-venv-path)
           (add-to-list 'poetry-venv-list (cons poetry-project-path poetry-venv-path)))))
      (((default t)) (pyvenv-deactivate)))))

(add-to-list 'window-buffer-change-functions 'my/track-python-virtualenv)
#+end_src

åŒæ—¶å¯ç”¨ pylint å’Œ pyright-lsp ã€‚

#+begin_src emacs-lisp
(defvar-local my/flycheck-local-cache nil)

(defun my/flycheck-checker-get (fn checker property)
  (or (alist-get property (alist-get checker my/flycheck-local-cache))
      (funcall fn checker property)))

(advice-add 'flycheck-checker-get :around 'my/flycheck-checker-get)

(add-hook 'lsp-managed-mode-hook
          (lambda ()
            (when (and (string= nil (file-remote-p default-directory))
                       (derived-mode-p 'python-mode))
              (setq my/flycheck-local-cache '((lsp . ((next-checkers . (python-pylint)))))))))

#+end_src
